let
  resourcesByType = (import ../parsetf.nix ./.state { }).resourcesByType;

  droplets = resourcesByType "digitalocean_droplet";

  mkOpEnergy = resource: { modulesPath, lib, name, pkgs, ... }:
  let
    instance_configuration = ./. + "/.state/${resource.values.ipv4_address}/configuration.nix";
    # import psk from out-of-git file
    # TODO: switch to secrets-manager and change to make it more secure
    bitcoind-signet-rpc-psk = builtins.readFile ( ./. + "/.state/${resource.values.ipv4_address}/private/bitcoind-signet-rpc-psk.txt");
    # TODO: refactor to autogenerate HMAC from the password above
    bitcoind-signet-rpc-pskhmac = builtins.readFile ( ./. + "/.state/${resource.values.ipv4_address}/private/bitcoind-signet-rpc-pskhmac.txt");
    op-energy-db-psk-signet = builtins.readFile ( ./. + "/.state/${resource.values.ipv4_address}/private/op-energy-db-psk-signet.txt");
  in
  {
    imports = lib.optional (builtins.pathExists ./do-userdata.nix) ./do-userdata.nix
            ++ [
      instance_configuration
      # custom module for already existing electrs derivation
      ../overlays/electrs-overlay/module.nix
      # custom module for op-energy
      ../module.nix
    ];
    deployment.targetHost = resource.values.ipv4_address;
    deployment.targetUser = "root";
    networking.hostName = resource.values.name;
    system.stateVersion = "21.05";

    #
    # and here we are enabling op-energy service. this option is being defined in `module.nix`
    services.op-energy-backend = {
# keeping testnet commented to have testnet ports in quick access
#      testnet = {
#        db_user = "topenergy";
#        db_name = "topenergy";
#        db_psk = op-energy-db-psk-testnet;
#        config = ''
#          {
#            "MEMPOOL": {
#              "NETWORK": "testnet",
#              "BACKEND": "electrum",
#              "HTTP_PORT": 8997,
#              "API_URL_PREFIX": "/api/v1/",
#              "POLL_RATE_MS": 2000
#            },
#            "CORE_RPC": {
#              "USERNAME": "top-energy",
#              "PASSWORD": "${bitcoind-testnet-rpc-psk}",
#              "PORT": 18332
#            },
#            "ELECTRUM": {
#              "HOST": "127.0.0.1",
#              "PORT": 60001,
#              "TLS_ENABLED": false
#            },
#            "DATABASE": {
#              "ENABLED": true,
#              "HOST": "127.0.0.1",
#              "PORT": 3306,
#              "DATABASE": "topenergy",
#              "USERNAME": "topenergy",
#              "PASSWORD": "${op-energy-db-psk-testnet}"
#            },
#            "STATISTICS": {
#              "ENABLED": true,
#              "TX_PER_SECOND_SAMPLE_PERIOD": 150
#            }
#          }
#        '';
#      };
      signet = {
        db_user = "sopenergy";
        db_name = "sopenergy";
        db_psk = op-energy-db-psk-signet;
        config = ''
          {
            "MEMPOOL": {
              "NETWORK": "signet",
              "BACKEND": "electrum",
              "HTTP_PORT": 8995,
              "API_URL_PREFIX": "/api/v1/",
              "POLL_RATE_MS": 2000
            },
            "CORE_RPC": {
              "USERNAME": "sop-energy",
              "PASSWORD": "${bitcoind-signet-rpc-psk}",
              "PORT": 38332
            },
            "ELECTRUM": {
              "HOST": "127.0.0.1",
              "PORT": 60601,
              "TLS_ENABLED": false
            },
            "DATABASE": {
              "ENABLED": true,
              "HOST": "127.0.0.1",
              "PORT": 3306,
              "DATABASE": "sopenergy",
              "USERNAME": "sopenergy",
              "PASSWORD": "${op-energy-db-psk-signet}"
            },
            "STATISTICS": {
              "ENABLED": true,
              "TX_PER_SECOND_SAMPLE_PERIOD": 150
            }
          }
        '';
      };
    };
    # enable op-energy-frontend service
    services.op-energy-frontend = {
      enable = true;
      signet_enabled = true;
    };

    # enable electrs service
    services.electrs = {
      signet = { # signet instance
        db_dir = "/mnt/electrs-signet";
        cookie_file = "/mnt/bitcoind-signet/signet/.cookie";
        blocks_dir = "/mnt/bitcoind-signet/signet/blocks";
        network = "signet";
        rpc_listen = "127.0.0.1:60601";
        daemon_rpc_addr = "127.0.0.1:38332";
      };
    };

    # bitcoind signet instance
    services.bitcoind.signet = {
      enable = true;
      dataDir = "/mnt/bitcoind-signet";
      extraConfig = ''
        txindex = 1
        server=1
        listen=1
        discover=1
        rpcallowip=127.0.0.1
        # those option affects memory footprint of the instance, so changing the default value
        # will affect the ability to shrink the node's resources.
        # default value is 450 MiB
        # dbcache=3700
        # default value is 125, affects RAM occupation
        # maxconnections=1337
        signet = 1
        [signet]
      '';
      rpc.users = {
        sop-energy = {
          name = "sop-energy";
          passwordHMAC = "${bitcoind-signet-rpc-pskhmac}";
        };
      };
    };

    # List packages installed in system profile. To search, run:
    # $ nix search wget
    environment.systemPackages = with pkgs; [
      screen
      atop # process monitor
      tcpdump # traffic sniffer
      iftop # network usage monitor
    ];
    # Enable the OpenSSH daemon.
    services.openssh.enable = true;

    # Open ports in the firewall.
    networking.firewall.allowedTCPPorts = [
      22
      80
    ];
    # networking.firewall.allowedUDPPorts = [ ... ];
    # Or disable the firewall altogether.
    networking.firewall.enable = true;
    networking.firewall.logRefusedConnections = false; # we are not interested in a logs of refused connections
  };

in
{
  network = {
    pkgs = import
      (builtins.fetchGit {
        name = "nixos-21.05-small-2021-10-30";
        url = "https://github.com/nixos/nixpkgs";
        ref = "refs/heads/nixos-21.05-small";
        rev = "6c0c30146347188ce908838fd2b50c1b7db47c0c";
      })
      { };
  };
} //
builtins.listToAttrs (map (r: { name = r.values.name; value = mkOpEnergy r; }) droplets)